plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'//用来推送aar到jitpack
    id 'pl.allegro.tech.build.axion-release' version '1.13.6'//用来发行aar版本的插件
}

//https://blog.csdn.net/java04/article/details/123574708
def properties = new Properties()
properties.load(rootProject.file("local.properties").newInputStream())
def gitUser = properties.getProperty("gitUser")
def gitPassWord = properties.getProperty("gitPassWord")
println 'gitUser=' + gitUser+"gitPassWord="+gitPassWord

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.centerzx'//插件id，格式：com.gitee/github.用户名
                artifactId = 'ShapeBlurView'//插件名称
                version = '1.0.3'//版本号
                //引用使用格式：implementation 'com.github.centerzx:ShapeBlurView:1.0.0'
            }
        }
        //jitpack推送授权
        repositories {
            maven {
                name('jitpack')
                url "https://jitpack.io"
                credentials {
                    username gitUser
                    password gitPassWord
                }
            }
        }

    }
}

//release插件，https授权
scmVersion{
    repository{
        customUsername = gitUser
        customPassword = gitPassWord
    }
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.0.3'
    }

    buildFeatures {
        compose = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    compileOnly 'androidx.appcompat:appcompat:1.4.1'

    implementation("androidx.compose.ui:ui:1.1.0")
    implementation("androidx.compose.material:material:1.1.0")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0")
    implementation("androidx.compose.ui:ui-tooling-preview:1.1.0")
    debugImplementation("androidx.compose.ui:ui-tooling:1.1.0")
}